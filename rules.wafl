
# Simple initial facts
This bot is doing well

This bot is called Computer


# Greetings commands
"bring yourself online"
  SAY Hello there!



# Time commands
the user asks for the time
  time = get_time()
  SAY the time is {time}


"What day is it today"
  date = get_date()
  SAY Today is {date}

# Shopping list

item = what does the user want to add to the shopping list?
  add_shopping_list(item)
  SAY {item} has been added to the list
  ! _ask_another_item
  items = get_shopping_list_in_english()
  SAY the shopping list now contains: {items}

item = what does the user want to remove from the shopping list?
  remove_from_shopping_list(item)
  items = get_shopping_list_in_english()
  SAY the shopping list now contains: {items}

_ask_another_item
  does the user want to add another item
  item = what do you want to add to the shopping list
  add_shopping_list(item)
  SAY {item} has been added to the list
  _ask_another_item

"remove an item from the shopping list"
  item = What do you want to remove?
  remove_from_shopping_list(item)
  items = get_shopping_list_in_english()
  SAY the shopping list now contains: {items}

the user wants to delete the shopping list
  Do you want to delete the current shopping list
  reset_shopping_list()
  SAY The shopping list has been deleted

the user wants to know what is in the shopping list
  items = get_shopping_list_in_english()
  SAY The shopping list contains: {items}

"What should I buy"
  the user wants to know what is in the shopping list


# Check for trains

"How is the {linename} line"
  linename = which line do you want to check?
  normname = normalize_name(linename)
  SAY Checking now
  check_tfl_line(normname)

"Is the {linename} running?"
  linename = which line do you want to check?
  normname = normalize_name(linename)
  SAY Checking now
  check_tfl_line(normname)

the user asks about disruptions on a London tube line
  linename = which line do you want to check?
  normname = normalize_name(linename)
  SAY Checking now
  check_tfl_line(normname)

"Is the {linename} running?"
  linename = which line do you want to check?
  normname = normalize_name(linename)
  SAY Checking now
  check_tfl_line(normname)

linename = which line is running?
  normname = normalize_name(linename)
  SAY Checking now
  check_tfl_line(normname)

is the overground running?
  SAY Checking now
  check_tfl_line("overground")


# Weather

"How is the weather"
  check_today_weather()

"How is the weather today"
  check_today_weather()

"How is the weather tomorrow"
  check_tomorrow_weather()


# End the conversation

_close
  close_conversation()

the user believes they don't need anything else
  SAY ok
  _close

the user says good bye
  SAY good bye!
  _close

the user says shut up
  SAY ok
  _close

the user asks this bot to be silent
  _close

the user says: "thank you this is all"
  _close

# Interruptions


the user does not want to continue the task
  ! Do you want to continue
  close_task()

the user wants to stop
  ! Do you want to continue
  close_task()